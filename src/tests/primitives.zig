const std = @import("std");
const root = @import("../root.zig");

test "canonical unsigned varints round trip" {
    const values = [_]u32{ 0, 1, 127, 128, 16384, std.math.maxInt(u32) };
    for (values) |expected| {
        var storage: [10]u8 = undefined;
        var writer = root.Writer.init(&storage);
        try writer.writeVarU32(expected);
        var reader = try root.Reader.init(writer.written(), .{});
        try std.testing.expectEqual(expected, try reader.readVarU32());
        try reader.finish();
    }
}

test "signed zigzag boundaries round trip" {
    const values = [_]i32{ std.math.minInt(i32), -1, 0, 1, 127, 128, std.math.maxInt(i32) };
    for (values) |expected| {
        var storage: [10]u8 = undefined;
        var writer = root.Writer.init(&storage);
        try writer.writeVarI32(expected);
        var reader = try root.Reader.init(writer.written(), .{});
        try std.testing.expectEqual(expected, try reader.readVarI32());
    }
}

test "malformed and non-canonical varints are rejected" {
    const cases = [_][]const u8{ &.{0x80}, &.{ 0x80, 0x00 }, &.{ 0xff, 0xff, 0xff, 0xff, 0x10 } };
    for (cases) |bytes| {
        var reader = try root.Reader.init(bytes, .{});
        try std.testing.expectError(if (bytes.len == 2) error.NonCanonicalVarInt else if (bytes.len == 1) error.EndOfStream else error.VarIntOverflow, reader.readVarU32());
    }
}

test "limits and UTF-8 are enforced without allocation" {
    var oversized = try root.Reader.init(&.{ 4, 't', 'e', 's', 't' }, .{ .max_string_bytes = 3 });
    try std.testing.expectError(error.LimitExceeded, oversized.readString());
    var invalid = try root.Reader.init(&.{ 2, 0xc3, 0x28 }, .{});
    try std.testing.expectError(error.InvalidUtf8, invalid.readString());
}
test "chunk subchunk sound byte-float and colour fixtures" {
    var storage: [128]u8 = undefined;
    var w = root.Writer.init(&storage);
    try w.writeChunkPosition(.{ .x = -2, .z = 300 });
    try w.writeSubChunkPosition(.{ .x = 1, .y = -2, .z = 3 });
    try w.writeSoundPosition(.{ .x = 1.25, .y = -2.5, .z = 3.0 });
    try w.writeByteFloat(-90.0);
    try w.writeRgba(.{ .r = 1, .g = 2, .b = 3, .a = 4 });
    try w.writeBeArgb(.{ .r = 1, .g = 2, .b = 3, .a = 4 });

    var r = try root.Reader.init(w.written(), .{});
    try std.testing.expectEqual(root.ChunkPosition{ .x = -2, .z = 300 }, try r.readChunkPosition());
    try std.testing.expectEqual(root.SubChunkPosition{ .x = 1, .y = -2, .z = 3 }, try r.readSubChunkPosition());
    try std.testing.expectEqual(root.Vec3f{ .x = 1.25, .y = -2.5, .z = 3.0 }, try r.readSoundPosition());
    try std.testing.expectEqual(@as(f32, 270.0), try r.readByteFloat());
    try std.testing.expectEqual(root.Rgba{ .r = 1, .g = 2, .b = 3, .a = 4 }, try r.readRgba());
    try std.testing.expectEqual(root.Rgba{ .r = 1, .g = 2, .b = 3, .a = 4 }, try r.readBeArgb());
    try r.finish();
}

test "float-backed compact encodings reject non-finite values" {
    var storage: [32]u8 = undefined;
    var w = root.Writer.init(&storage);
    try std.testing.expectError(error.InvalidValue, w.writeByteFloat(std.math.nan(f32)));
    try std.testing.expectError(error.InvalidValue, w.writeSoundPosition(.{ .x = std.math.inf(f32), .y = 0, .z = 0 }));
    try std.testing.expectEqual(@as(usize, 0), w.written().len);
}
